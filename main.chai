// Module Dependencies
require("lib/StateMachine")
require("GameState")
require("MainMenuState")
require("Pipe")
require("Bird")

global bird
global assets
global pipes = List()
global pipetimer = 0.0f
global pipetimerCountdown = 1.4f
global score = 0
global openingSizeStart = 30.0f
global openingSize = openingSizeStart
global openingSizeRangeStart = 5.0f
global openingSizeRange = openingSizeRangeStart
global flapTimer = 0.0f
global flapTimerMax = 0.10f
global landTop = 0
global landX = 0
global landWidth = 0
global landHeight = 0
global skyX = 0
global skyWidth = 0
global skyHeight = 0
global highscore = 0
global states
global cheatNoClip = false
global cheatFly = false
global windowWidth = 320
global windowHeight = 240

/**
 * Configuration for ChaiLove.
 */
def conf(t) {
	t.identity = "floppybirdlite"
	t.version = "0.28.0" // Version of ChaiLove
	t.window.title = "Floppy Bird Lite";
	t.window.width = 320;
	t.window.height = 240;
}

/**
 * Load all the game assets and files.
 */
def load() {
	// Set up the State Manager.
	states = StateMachine()

	// Set up the background.
	love.graphics.setBackgroundColor(78, 192, 202)

	// Load all asset files.
	love.filesystem.load("assets.chai")

	// Construct the asxsets.
	bird = Bird()

	landWidth = assets["land"].getWidth()
	landHeight = assets["land"].getHeight()
	landTop = windowHeight - landHeight
	skyWidth = assets["sky"].getWidth()
	skyHeight = assets["sky"].getHeight()

	// Set up the Game States
	states.add("menu", MainMenuState())
	states.add("game", GameState())
	states.set("menu")

	// Bring all the game entities to ground zero.
	reset()
}

/**
 * Callback; Reset all entities and content.
 */
def reset() {
	pipes.clear()
	addPipe()
	bird.reset()
	score = 0
	openingSize = openingSizeStart
	openingSizeRange = openingSizeRangeStart
	pipetimer = 0.0f
	flapTimer = 0.0f
	landX = 0
}

/**
 * Add a pipe to the pipe queue.
 */
def addPipe() {
	// Find out where the pipes should be positioned.
	var pipeTubeSize = 13
	var opening = love.math.random(pipeTubeSize + openingSize, landTop - pipeTubeSize - openingSize)
	var openSize = openingSize + love.math.random(-openingSizeRange, openingSizeRange)

	// Add the pipes to the game.
	pipes.push_back_ref(Pipe(opening, true, openSize))
	pipes.push_back_ref(Pipe(opening, false, openSize))
}

/**
 * Draw all entities on the screen.
 */
def draw() {
	// Draw the background (land and sky).
	var land := assets["land"]
	for (var i = 0; (i - 1) * landWidth <= windowWidth; ++i) {
		love.graphics.draw(land, i * landWidth - landX, windowHeight - land.getHeight())
	}
	if (love.config.options["highquality"]) {
		var sky := assets["sky"]
		for (var i = 0; (i - 1) * skyWidth <= windowWidth; ++i) {
			love.graphics.draw(sky, i * skyWidth - skyX, landTop - sky.getHeight())
		}
	}

	// Draw the Bird
	bird.draw()

	// Draw all the pipes
	for (pipe : pipes) {
		pipe.draw()
	}

	// Draw the current game state.
	states.draw()

	// Print the score.
	if (score > 0) {
		love.graphics.setFont(assets["font-big"])
		var scoretext = to_string(score)
		var scorewidth = assets["font-big"].getWidth(scoretext)
		love.graphics.print(to_string(score), windowWidth / 2 - scorewidth / 2, 15)
	}

	// FPS
	love.graphics.setFont(assets["font-pixel"])
	love.graphics.print("FPS " + to_string(love.timer.getFPS()), 5, windowHeight - 10)

	// Highscore
	love.graphics.print("BEST " + to_string(highscore), windowWidth - 75, windowHeight - 10)
}

/**
 * Callback; Update the game.
 */
def update(delta) {
	states.update(delta)
}

/**
 * Callback; Called when a joystick button is hit.
 */
def joystickpressed(joy, button) {
	states.joystickpressed(button)
}

/**
 * Adds a point to the score.
 */
def addScore() {
	score += 1
	assets["point"].play()
	// Update the highscore.
	if (score > highscore) {
		highscore = score
	}
}

def isMusicPlaying() {
	if (assets["music1"].isPlaying()) {
		return true
	}
	if (assets["music2"].isPlaying()) {
		return true
	}
	if (assets["music3"].isPlaying()) {
		return true
	}
	return false
}

/**
 * Callback; Save the current state as a JSON string.
 */
def savestate() {
	var pipeInfo = []
	for (pipe : pipes) {
		pipeInfo.push_back(pipe.serialize())
	}
	var info = [
		"score": score,
		"highscore": highscore,
		"bird": bird.serialize(),
		"currentState": states.getCurrentName(),
		"pipes": pipeInfo,
		"openingSize": openingSize,
		"openingSizeRange": openingSizeRange,
		"pipetimer": pipetimer,
		"flapTimer": flapTimer,
		"landX": landX,
		"skyX": skyX,
		"cheatNoClip": cheatNoClip,
		"cheatFly": cheatFly
	]
	return to_json(info)
}

/**
 * Callback; Load the state from the given JSON string.
 */
def loadstate(data) {
	// Retrieve the state data.
	var info = from_json(data)
	highscore = info["highscore"]
	bird.deserialize(info["bird"])
	states.currentState = info["currentState"]
	score = info["score"]
	pipes.clear()
	for (pipe : info["pipes"]) {
		pipes.push_back(Pipe(100, pipe["top"], 100).deserialize(pipe))
	}
	openingSize = info["openingSize"]
	openingSizeRange = info["openingSizeRange"]
	pipetimer = info["pipetimer"]
	flapTimer = info["flapTimer"]
	landX = info["landX"]
	skyX = info["skyX"]
	cheatNoClip = info["cheatNoClip"]
	cheatFly = info["cheatFly"]

	return true
}

/**
 * Callback; Reset the state of all the cheats.
 */
def cheatreset() {
	cheatNoClip = false
	cheatFly = false
}

/**
 * Callback; Enable or disable a cheat code.
 */
def cheatset(index, enabled, code) {
	if (code == "noclip") {
		cheatNoClip = enabled
	}
	if (code == "fly") {
		cheatFly = enabled
	}
}
