class Bird {

	def serialize() {
		return [
			"x": this.x,
			"y": this.y,
			"yspeed": this.yspeed,
			"speed": this.speed
		]
	}

	def deserialize(obj) {
		this.x = obj["x"]
		this.y = obj["y"]
		this.yspeed = obj["yspeed"]
		this.speed = obj["speed"]
	}

	def Bird() {
		this.collisionPadding = 2
		this.x = 53
		this.speed = 100
		this.y = 50
		this.yspeed = 0
		this.ygravity = 500
		this.animationslide = 2
		this.animtimer = 0.0f

		this.img = []
		for (var i = 0; i <= 3; ++i) {
			var imgId = i == 3 ? 1 : i;
			this.img.push_back_ref(love.graphics.newImage("assets/bird" + to_string(imgId) + ".png"))
		}
		this.width = this.img[0].getWidth()
		this.height = this.img[0].getHeight()
	}

	def draw() {
		love.graphics.draw(this.img[this.animationslide], this.x, this.y)
	}

	def update(delta) {
		this.animtimer += delta

		if (this.animtimer > 0.12f) {

			this.animtimer = 0
			this.animationslide += 1
			if (this.animationslide > 3) {
				this.animationslide = 3

				if (cheatFly) {
					this.flap(false)
				}
			}
		}

		this.yspeed += this.ygravity * delta
		this.y += this.yspeed * delta

		if (this.y + this.height > landTop) {
			this.y = landTop - this.height
			assets["die"].play()
			return false
		}

		if (this.yspeed < 0 && this.y < 0) {
			this.yspeed = 0
			this.y = this.collisionPadding
		}

		return true
	}

	def flap(mute) {
		this.yspeed = -140
		this.animationslide = 0
		if (mute == false) {
			assets["wing"].play()
		}
	}

	def reset() {
		this.y = windowHeight / 3
		this.flap(true)
	}
}
